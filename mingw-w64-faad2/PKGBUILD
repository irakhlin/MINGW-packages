# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=faad2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.8.1
pkgrel=1
pkgdesc="ISO AAC audio decoder (mingw-w64)"
arch=('any')
url="http://www.audiocoding.com/"
license=('GPL')
options=('staticlibs' 'strip')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("yasm" "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(https://downloads.sourceforge.net/sourceforge/faac/${_realname}-${pkgver}.tar.bz2
        0002-no-undefined.mingw.patch
        0003-frontend.mingw.patch)
sha256sums=('f4042496f6b0a60f5ded6acd11093230044ef8a2fd965360c1bbd5b58780933d'
            'a69c59ba6b1ef26f3ef7872b1946641fb1e25e47a3caf6199effc2ca93dc4ba0'
            '400544706cb409483f08db0f9a2ff37330476047f6648c11d7a4baccf994edb1')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0002-no-undefined.mingw.patch"
  patch -p1 -i "${srcdir}/0003-frontend.mingw.patch"

  WANT_AUTOMAKE=latest autoreconf --force --install
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
